#############
# Providers #
#############

provider "aws" {
  region = "${var.region}"
}

## NOTE: Uncomment if you change the main provider region to anything except "us-east-1"
# provider "aws" {
#   region  = "us-east-1"
#   alias   = "virginia"
# }

#############
# Variables #
#############

variable "region" {
  # NOTE: If changed, you must uncomment virginia provider alias above
  default = "us-east-1"
}

variable "app_name" {
  default = "amplify2terraform"
}

variable "namespace" {
  default = "amp2terr-dev"
}

variable "stage" {
  # e.g. development, testing, production
  default = "development"
}

variable "notifier_fn_arn" {
  # Output from serverless
  default = "arn:aws:lambda:us-east-2:659400527649:function:services-dev-notifier"
}

variable "site_domain" {
  # Your Domain Name used for S3 bucket + Cloudfront distro
  default = "example.com"
}

variable "cognito_role_external_id" {
  # Some unique ID that cognito can use for SMS verification
  default = "amp2terr-unique-238478238"
}

###########################
# Additional Data Sources #
###########################

# Used by Cloudfront distribution
data "aws_acm_certificate" "website" {
  # NOTE: uncomment if you changed default provider region
  # provider    = "aws.virginia"
  domain = "${var.site_domain}"

  statuses    = ["ISSUED"]
  most_recent = true
}

data "aws_route53_zone" "website" {
  name = "${var.site_domain}."
}

###########
# Outputs #
###########

locals {
  awsexports = <<AWSEXPORTS

// Warning: This file is automatically generated. It will be overwritten.
// Stage: ${var.stage}

const awsmobile = {
    "aws_project_region": "${var.region}",
    "aws_appsync_region": "${var.region}",
    "aws_cognito_region": "${var.region}",
    "aws_cognito_identity_pool_id": "${aws_cognito_identity_pool.main.id}",
    "aws_user_pools_id": "${aws_cognito_user_pool.main.id}",
    "aws_user_pools_web_client_id": "${aws_cognito_user_pool_client.web.id}",
    "aws_appsync_graphqlEndpoint": "${aws_appsync_graphql_api.main.uris["GRAPHQL"]}",
    "aws_appsync_authenticationType": "AMAZON_COGNITO_USER_POOLS",
};

export default awsmobile;
AWSEXPORTS
}

output "aws-exports-file" {
  value = "${local.awsexports}"
}

output "user_pool_id" {
  value = "${aws_cognito_user_pool.main.id}"
}
